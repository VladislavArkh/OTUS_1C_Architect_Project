  #Область Настройки

Функция ПолучитьНастройкиПодключенияИзРегистра(ДопУсловие = "") Экспорт  
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	RMQ.Адрес КАК Адрес,
		|	RMQ.Порт КАК Порт,
		|	RMQ.Логин КАК Логин,
		|	RMQ.Пароль КАК Пароль,
		|	RMQ.ВиртуальныйХост КАК ВиртуальныйХост,
		|	RMQ.ТочкаОбмена КАК ТочкаОбмена,
		|	RMQ.ИмяОчереди КАК ИмяОчереди,
		|	RMQ.Включен КАК Включен,
		|	RMQ.Таймаут КАК Таймаут,
		|	RMQ.КлючМаршрута КАК КлючМаршрута
		|ИЗ
		|	РегистрСведений.КАБНастройкиПодключенияКRebbitMQ КАК RMQ
		|ГДЕ
		|	RMQ.Включен
		|	И &ДопУсловие";
	
	Запрос.УстановитьПараметр("ДопУсловие", Истина);
	
	Если ЗначениеЗаполнено(ДопУсловие) Тогда
	    Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДопУсловие", "RMQ.Описание = &Описание");
		Запрос.УстановитьПараметр("Описание", Строка(СокрЛП(ДопУсловие)));
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Настройки = Новый Структура("Адрес,Порт,Логин,Пароль,ВиртуальныйХост,ТочкаОбмена,ИмяОчереди,Таймаут,КлючМаршрута");
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Настройки, ВыборкаДетальныеЗаписи);
	КонецЕсли;
	
	Возврат Настройки;
	
КонецФункции

Функция ПолучитьНастройкиПодключения(Адрес, Порт, Логин, Пароль, ВиртуальныйХост,ТочкаОбмена, ИмяОчереди, Таймаут) Экспорт  
	
	Возврат Новый Структура("Адрес,Порт,Логин,Пароль,ВиртуальныйХост,ТочкаОбмена,ИмяОчереди,Таймаут",
			Адрес, Порт, Логин, Пароль, ВиртуальныйХост, ТочкаОбмена, ИмяОчереди, Таймаут);
КонецФункции

#КонецОбласти

#Область Компонента 

Функция ПолучитьКомпоненту() Экспорт 

	КлиентКомпоненты = Неопределено;
	Если Не ИнициализироватьКомпоненту(КлиентКомпоненты) Тогда
		
		РезультатПодключения = ПодключитьВнешнююКомпоненту("ОбщийМакет.PinkRabbitMQ", "BITERP", ТипВнешнейКомпоненты.Native);
		
		Если РезультатПодключения Тогда
			ИнициализироватьКомпоненту(КлиентКомпоненты);
		КонецЕсли;    	
	КонецЕсли;
	
	Возврат КлиентКомпоненты;
	
КонецФункции

Функция ИнициализироватьКомпоненту(Компонента)
	
	Попытка
		Компонента  = Новый("AddIn.BITERP.PinkRabbitMQ");
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

#КонецОбласти
